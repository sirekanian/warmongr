plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.kotlin.kapt'
}

android {
    namespace 'com.sirekanian.acf'
    compileSdk 33
    defaultConfig {
        applicationId "com.sirekanian.warmongr"
        minSdk 21
        targetSdk 33
        versionCode appVersionCode as int
        versionName appVersionName
        archivesBaseName = "$applicationId-$versionName-$versionCode"
        vectorDrawables {
            useSupportLibrary true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion composeCompilerVersion
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lint {
        warningsAsErrors true
    }
}

dependencies {

    // androidx compose
    implementation "androidx.compose.material:material:$composeMaterialVersion"
    implementation 'androidx.activity:activity-compose:1.6.1'

    // androidx room
    //noinspection GradleDependency
    implementation "androidx.room:room-runtime:$roomVersion"
    //noinspection GradleDependency
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // ktor
    implementation "io.ktor:ktor-client-cio:$ktorVersion"
    implementation "io.ktor:ktor-client-content-negotiation:$ktorVersion"
    implementation "io.ktor:ktor-client-encoding:$ktorVersion"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktorVersion"

}
